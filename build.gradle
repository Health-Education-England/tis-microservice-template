plugins {
  id "java"
  id "org.springframework.boot" version "2.3.5.RELEASE"
  id "io.spring.dependency-management" version "1.0.10.RELEASE"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.0"
}

// TODO: Update group to end with "admin" or "trainee".
group = "uk.nhs.hee.tis"
version = "0.0.1"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  implementation "org.mapstruct:mapstruct:1.4.1.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.4.1.Final"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

  // Sentry reporting
  ext.sentryVersion = "3.1.2"
  implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
  implementation "io.sentry:sentry-logback:$sentryVersion"
}

checkstyle {
  toolVersion = "8.29"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    // TODO: Update sonar.projectKey to real value.
    property "sonar.projectKey", "Health-Education-England_tis-microservice-template"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
